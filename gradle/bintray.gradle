apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
version = VERSION_NAME
group = GROUP

task androidSourcesJar(type: Jar) {
    from android.sourceSets.main.java.source
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                artifact bundleRelease
                artifact androidSourcesJar
                groupId project.group
                artifactId POM_NAME
                version project.version

                pom.withXml {
                    def node = asNode() as Node
                    def dependenciesNode = node.appendNode('dependencies')
                    def addDependencies = { Configuration config, String scope ->
                        config.allDependencies.matching { it.name != 'unspecified' }.all {
                            def dependency = dependenciesNode.appendNode('dependency')
                            dependency.appendNode('groupId', it.group)
                            dependency.appendNode('artifactId', it.name)
                            dependency.appendNode('version', it.version)
                            dependency.appendNode('scope', scope)
                        }
                    }
                    addDependencies(configurations.implementation, 'compile')
                    addDependencies(configurations.compileOnly, 'provided')
                    dependenciesNode + {
                        resolveStrategy = DELEGATE_FIRST
                        description POM_DESCRIPTION
                        name POM_NAME
                        licenses {
                            license {
                                name "The Apache Software License, Version 2.0"
                                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                                distribution "repo"
                            }
                        }
                        developers {
                            developer {
                                id 'bilibili-app'
                                name 'Bilibili android app team'
                            }
                        }
                        organization {
                            name 'bilibili'
                            url 'https://github.com/Bilibili'
                        }
                        scm {
                            url "https://github.com/bilibili/xpref"
                        }
                    }

                }

            }
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : System.getenv('BINTRAY_API_KEY')
        publications = ['maven']
        override = true
        pkg {
            repo = 'maven'
            name = POM_NAME
            userOrg = 'bilibili'
            description = POM_DESCRIPTION
            publish = true
            publicDownloadNumbers = true
            licenses = ['Apache-2.0']
            labels = ['android', 'sharedpreferences']
            websiteUrl = 'https://github.com/bilibili/xpref'
            issueTrackerUrl = 'https://github.com/bilibili/xpref/issues'
            vcsUrl = 'https://github.com/bilibili/xpref.git'
            dryRun = false
            version {
                name = VERSION_NAME
                released = new Date()
                vcsTag = VERSION_NAME
            }
        }
    }
    tasks.bintrayUpload.dependsOn publishToMavenLocal
}